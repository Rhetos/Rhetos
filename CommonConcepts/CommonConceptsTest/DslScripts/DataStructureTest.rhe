Module TestDataStructure { DataStructure SimpleDataStructure1; }

Module TestDataStructure { DataStructure SimpleDataStructure2 { ShortString SimpleShortString; } }

Module TestDataStructure
{
	SqlQueryable SqlQueryable1 "SELECT ID = '5D089327-97EF-418D-A7DF-783D3873A5B4', i = 1, s = 'a'
        UNION ALL SELECT 'DB97EA5F-FB8C-408F-B35B-AD6642C593D7', 2, 'b'"
	{
		Integer i;
		ShortString s;
	}
	
	Entity CachingTestEntity { ShortString S; }
	SqlQueryable CachingTestView "SELECT * FROM TestDataStructure.CachingTestEntity" { ShortString S; }
	
	Entity Parent;
	Entity Child
	{
		Reference Parent  { Detail; }
	}
    
    Entity ReferenceView { Reference SqlQueryable1; }
}

Module TestExtension
{
    AutodetectSqlDependencies;
    
	SqlQueryable SqlQueryableBase1 "SELECT ID = '5D089327-97EF-418D-A7DF-783D3873A5B4', i = 1, s = 'a' UNION ALL SELECT 'DB97EA5F-FB8C-408F-B35B-AD6642C593D7', 2, 'b'"
	{
		Integer i;
		ShortString s;
	}

	SqlQueryable SqlQueryableExtenson1 "SELECT ID, info = CAST(i AS varchar(20)) + '-' + s FROM TestExtension.SqlQueryableBase1"
	{
		Extends TestExtension.SqlQueryableBase1;
		ShortString info;
	}

	Entity Old1
	{
		Integer i;
		ShortString s;
	}
	
	SqlView Old1View 'SELECT * FROM TestExtension.Old1';
	
	LegacyEntity Legacy1 'TestExtension.Old1' 'TestExtension.Old1View'
	{
		Integer i;
		ShortString s;
	}

	Entity EntityExtension1
	{
		Extends TestExtension.Legacy1;
		ShortString info;
	}
	
	Entity SimpleBase { ShortString Name; }
	Entity SimpleExtension { Extends TestExtension.SimpleBase; ShortString Name; }
	SqlQueryable MissingExtensionSql "SELECT sb.ID, Name = sb.Name + 'Sql' FROM TestExtension.SimpleBase sb WHERE sb.Name NOT LIKE '%missing%'" { Extends TestExtension.SimpleBase; ShortString Name; }
	QueryableExtension MissingExtensionCs TestExtension.SimpleBase '(source, repository) => source
		.Where(item => !item.Name.Contains("missing"))
		.Select(item => new Common.Queryable.TestExtension_MissingExtensionCs { ID = item.ID, Name = item.Name + "Cs" })' { ShortString Name; }
	SqlQueryable InvalidExtension 'SELECT ID, Data = 1/0 FROM TestExtension.SimpleBase' { Extends TestExtension.SimpleBase; Integer Data; }
}

Module TestEntity
{
	Entity Principal
	{
		ShortString Name;
	}
	
	Entity Claim
	{
		ShortString ClaimResource;
		ShortString ClaimRight;
	}
	
	Entity Permission
	{
		Reference Principal TestEntity.Principal { CascadeDelete; }
		Reference Claim TestEntity.Claim { CascadeDelete; }
		Bool IsAuthorized;
	}

	Entity BaseEntity
	{
		ShortString Name;
	}

	Entity Extension
	{
		Extends TestEntity.BaseEntity;
		ShortString Title;
	}
	
	Entity Child
	{
		ShortString Name;
		Reference Parent TestEntity.BaseEntity { Detail; }
	}
	
	Browse ChildBrowse TestEntity.Child
	{
		ShortString Name { From 'Name'; }
		ShortString ParentName { From 'Parent.Name'; }
	}
    
    Entity Large
    {
        LongString Text;
    }
    
    Entity UniqueEntity
    {
        ShortString Name { Required; Unique; }
        ShortString Data;
    }
}

Module TestBrowse
{
	Entity Other
	{
		ShortString Name;
	}
	
	Entity Source
	{
		ShortString Code;
		Reference Ref TestBrowse.Other;
	}
	
	Browse SF TestBrowse.Source
	{
		ShortString RefName { From 'Ref.Name'; }
	}
	
	Browse SFMulti TestBrowse.Source
	{
		ShortString Code1 { From Code; }
		ShortString Code2 { From Code; }
		ShortString RefName1 { From 'Ref.Name'; }
		ShortString RefName2 { From 'Ref.Name'; }
	}
	
	//=================================
	
	Entity ParentBase
	{
		ShortString Name;
	}
	
	Entity Parent
	{
		Extends TestBrowse.ParentBase;
		ShortString Name;
	}
	
	Entity ParentExtension1
	{
		Extends TestBrowse.Parent;
		ShortString Name1;
	}
	
	Entity ParentExtension2
	{
		Extends TestBrowse.Parent;
		ShortString Name2;
	}
	
	Entity Complex
	{
		ShortString Code;
		Reference Ref TestBrowse.Parent;
	}

	Browse SFTake TestBrowse.Complex
	{
		Take Code;
		Take 'Ref.Name';
		Take RefName2 'Ref.Name';
		ShortString RefName3 { From 'Ref.Name'; }
		
		Take 'Ref.Base.Name';
		Take 'Ref.Extension_ParentExtension2.Name2';
		
		Take 'Ref.ID';
		Take ParentReference Ref;
	}
}

Module Test13
{
	AutodetectSqlDependencies;

	Entity Old1
	{
		Integer IDOld1;
		ShortString Name;
	}
	
	SqlView Old1View 'SELECT ID, Name FROM Test13.Old1';

	LegacyEntity Legacy1 'Test13.Old2' 'Test13.Old1View'
	{
		ShortString Name;

		SqlDependsOn Test13.Old2;
	}
	
	Entity Old2
	{
		Integer IDOld2;
		ShortString Name;
		Integer Old1ID;
        ShortString Same;
	}
	
	LegacyEntity Legacy2 'Test13.Old2' // view is automatically generated
	{
		ShortString NameNew { LegacyProperty 'Name'; }
		Reference Leg1 Test13.Legacy1 { LegacyProperty 'Old1ID' 'Test13.Old1' 'IDOld1'; }
        ShortString Same;
		
		SqlDependsOn Test13.Old2;
	}
	
	Entity Old3
	{
		Integer Num;
		ShortString Text;
	}
	
	LegacyEntity Legacy3 'Test13.Old3'
	{
		Integer NumNew { LegacyPropertyReadOnly 'Num'; }
		ShortString TextNew { LegacyProperty 'Text'; }
		
		SqlDependsOn Test13.Old3;
	}
	
	Parameter PatternFilter { ShortString Pattern; }
	FilterBy Legacy3.'Test13.PatternFilter'
		'(repos, filter) => repos.Test13.Legacy3.Query().Where(item => item.TextNew.Contains(filter.Pattern) || item.NumNew.CastToString().StartsWith(filter.Pattern)).ToArray()';
        
    Entity OldMultiParent { Integer Key1; ShortString Key2; ShortString Name; }
    Entity OldMultiChild { Integer ParentKey1; ShortString ParentKey2; ShortString Name; }
    LegacyEntity LegacyMultiParent 'Test13.OldMultiParent'
    {
        ShortString Name;
        SqlDependsOn Test13.OldMultiParent;
    }
    LegacyEntity LegacyMultiChild 'Test13.OldMultiChild'
    {
        Reference Parent Test13.LegacyMultiParent { LegacyProperty 'ParentKey1, ParentKey2' 'Test13.OldMultiParent' 'Key1, Key2'; }
        ShortString Name;
        SqlDependsOn Test13.OldMultiChild;
    }
}

Module TestBinary
{
	Entity E
	{
		Binary Blob;
	}
}

Module TestTypes
{
    Entity Simple
    {
        DateTime Start;
        Decimal Length;
    }
    
    // TestTypes.Reader is used to avoid TestTypes.Simple caching side effects.
    SqlQueryable Reader 'SELECT * FROM TestTypes.Simple'
    {
        AllPropertiesFrom TestTypes.Simple;
    }
}

Module TestUnique
{
	Entity R
	{
		ShortString S;
	}
	
	Entity E
	{
		ShortString S;
		Integer I;
		Reference R TestUnique.R;
		
		Unique TestUnique.E.S TestUnique.E.I TestUnique.E.R;
	}
	
	Entity Multi
	{
		ShortString S;
		Integer I;
		Reference R TestUnique.R;
		
		UniqueMultiple 'S R';
		UniqueMultiple 'I';
	}
	
	LegacyEntity LE 'TestUnique.E'
	{
		ShortString S { Unique; } // Should be checked in the application, not in database.
		Integer I;
	}
}

Module TestCloning
{
	Entity Parent
	{
		ShortString Name;
	}
	
	Entity Base
	{
		ShortString Name;
	}
	
	Entity Source
	{
		Extends TestCloning.Base;
		
		Integer Code { Unique; }
		ShortString Name { Required; }
		DateTime Start;
		Reference Parent TestCloning.Parent { Detail; }
		SqlIndexMultiple 'Start Parent';
	}
	
	Entity Clone1
	{
		PropertyFrom TestCloning.Source.Start;
	}
	
	Entity Clone2
	{
		PropertyFrom TestCloning.Source.Name;
		PropertyFrom TestCloning.Source.Parent;
		PropertyFrom TestCloning.Source.Start;
	}
	
	Entity Clone3
	{
		AllPropertiesWithCascadeDeleteFrom TestCloning.Source;
	}
}

Module TestDataStructure
{
	Entity SaveTesterBase
	{
		ShortString Name;
	}
	
	Entity SaveTester
	{
		Extends TestDataStructure.SaveTesterBase;
		ShortString Name;
		Integer Code;
		
		SaveMethod
		{
			LoadOldItems
			{
				Take Name;
				Take 'Base.Name';
			}
			
			OnSaveUpdate 'UnitTestingUpdate'
				'var changedItemsName = updatedOld
					.Zip(updated, (oldItem, newItem) => new { oldItem, newItem})
					// Safe universal property comparison:
					.Where(change => change.oldItem.Name == null && change.newItem.Name != null || change.oldItem.Name != null && !change.oldItem.Name.Equals(change.newItem.Name))
					.ToList();
				
				var updateIds = changedItemsName.Select(change => change.newItem.ID);
				var updateBaseItems = _executionContext.Repository.TestDataStructure.SaveTesterBase.Filter(updateIds);
				foreach (var baseItem in updateBaseItems)
					baseItem.Name += " updated";
				_executionContext.Repository.TestDataStructure.SaveTesterBase.Update(updateBaseItems);';
				
			OnSaveValidate 'UnitTestingValidate'
				'var lockedItemModifyName = updatedOld
					.Zip(updated, (oldItem, newItem) => new { oldItem, newItem})
					// Safe universal property comparison:
					.Where(change => change.oldItem.Name == null && change.newItem.Name != null || change.oldItem.Name != null && !change.oldItem.Name.Equals(change.newItem.Name))
					.Where(change => change.oldItem.BaseName != null && change.oldItem.BaseName.Contains("lock"))
					.SingleOrDefault();
				
				if (lockedItemModifyName != null)
					throw new Rhetos.UserException(string.Format(
						"It is not allow to modify locked item''s name ''{0}'' => ''{1}''.",
						lockedItemModifyName.oldItem.Name,
						lockedItemModifyName.newItem.Name));';
		}
	}
}

Module TestQueryable
{
	Parameter StartsWith
	{
		ShortString Prefix;
	}
	
	Entity Simple
	{
		ShortString Name;
		Query 'TestQueryable.StartsWith' 'parameter => Query().Where(item => item.Name.StartsWith(parameter.Prefix))';
	}
}

Module TestLazyLoad
{
	LazyLoadReferences;
	
	Entity Parent
	{
		ShortString Name;
		LinkedItems Simples TestLazyLoad.Simple.Parent;
	}
	
	Entity SimpleBase
	{
		ShortString Name;
	}
	
	Entity Simple
	{
		Extends TestLazyLoad.SimpleBase;
		Reference Parent;
	}
}
